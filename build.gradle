plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id "com.diffplug.spotless" version "6.20.0"
}

archivesBaseName = 'sequencetools'
group = 'uk.ac.ebi.ena.sequence'

version = '2.19.4'

sourceCompatibility = 1.8
targetCompatibility = 1.8

spotless {
    encoding 'UTF-8'
    lineEndings 'UNIX'
    java {
        licenseHeader '''/*
 * Copyright 2019-2024 EMBL - European Bioinformatics Institute
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this
 * file except in compliance with the License. You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software distributed under the
 * License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR
 * CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 */
'''
        importOrder 'java', 'javax', 'org', 'com', '', 'uk'
        removeUnusedImports()
        googleJavaFormat()
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://gitlab.ebi.ac.uk/api/v4/groups/enasequence/-/packages/maven"

        credentials(HttpHeaderCredentials) {
            name = "Private-Token"
            value = gitlab_private_token
        }
        authentication {
            header( HttpHeaderAuthentication )
        }
    }
    // For BioSamples client
    maven {
        url 'https://gitlab.ebi.ac.uk/api/v4/projects/2669/packages/maven'

        credentials(HttpHeaderCredentials) {
            name = "Private-Token"
            value = biosamples_gitlab_private_token
        }
        authentication {
            header(HttpHeaderAuthentication)
        }
    }
}

dependencies {
    api( 'uk.ac.ebi.ena.webin-cli:webin-cli-validator:1.7.8' ){
        exclude group: 'org.springframework.boot'
    }
    api(group: 'uk.ac.ebi.ena.taxonomy', name: 'webin-taxonomy-sdk', version: '1.0.10')

    implementation 'org.slf4j:slf4j-api:1.7.28'

    implementation 'commons-collections:commons-collections:3.2.2'
    implementation 'commons-dbutils:commons-dbutils:1.5'
    implementation 'org.apache.commons:commons-lang3:3.0'
    implementation 'org.apache.commons:commons-compress:1.20'

    implementation group: 'org.mapdb', name: 'mapdb', version: '3.0.10'

    testImplementation 'org.json:json:20131018'
    testImplementation "junit:junit:4.13.1"
    testImplementation 'org.easymock:easymock:4.2'

    testImplementation 'org.springframework.boot:spring-boot-starter:2.2.0.RELEASE'
    testImplementation 'org.springframework.boot:spring-boot-starter-jdbc:2.2.0.RELEASE'
    testImplementation ('org.springframework.boot:spring-boot-starter-test:2.2.0.RELEASE') {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }

    testRuntimeOnly'com.oracle.database.jdbc:ojdbc8:19.8.0.0'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

test {
    // Webin account
    if( System.env.webin_password ) {
        environment 'webin-password', System.env.webin_password
    } else {
        environment 'webin-password', webinPassword
    }

    if( System.env.webin_username ) {
        environment 'webin-username', System.env.webin_username
    } else {
        environment 'webin-username', webinUsername
    }

    // Biosamples Webin account
    if( System.env.biosamples_webin_password ) {
        environment 'biosamples-webin-password', System.env.biosamples_webin_password
    } else {
        environment 'biosamples-webin-password', biosamplesWebinPassword
    }

    if( System.env.biosamples_webin_username ) {
        environment 'biosamples-webin-username', System.env.biosamples_webin_username
    } else {
        environment 'biosamples-webin-username', biosamplesWebinUsername
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            url "https://gitlab.ebi.ac.uk/api/v4/projects/965/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = gitlab_private_token
            }
            authentication {
                header( HttpHeaderAuthentication )
            }
        }       
    }
}
